plugins {
    id 'java-library'
    id'org.graalvm.buildtools.native' version '0.10.2' // GraalVM plugin for Gradle
}


def tikaVersion = "2.9.2"
def numThreads = Runtime.getRuntime().availableProcessors()


group = 'ai.yobix'
version = "$tikaVersion-SNAPSHOT"


repositories {
    mavenCentral()
}

dependencies {
    // Tika uses slf4j, just use a nop logger to ignore all logging
    implementation("org.slf4j:slf4j-nop:2.0.11")
    // Some dependencies use log4j such as poi, route log4j back to slf4j
    // Had to use 3.0.0-beta2 because it is solves some issues with log4j to make it graalvm native friendly
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:3.0.0-beta2'

    implementation("org.apache.tika:tika-core:$tikaVersion")
    implementation "org.apache.tika:tika-parsers-standard:$tikaVersion" // Apache Tika parsers

    implementation("org.apache.tika:tika-parser-microsoft-module:$tikaVersion")
    implementation "org.apache.tika:tika-parser-pdf-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-html-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-apple-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-audiovideo-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-cad-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-crypto-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-font-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-html-commons:$tikaVersion"
    implementation "org.apache.tika:tika-parser-image-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-mail-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-miscoffice-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-news-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-ocr-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-pkg-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-text-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-xml-module:$tikaVersion"
    implementation "org.apache.tika:tika-parser-webarchive-module:$tikaVersion"
}

// We use java BellSoft 22 build because it supports awt on macos.
def graalVmVersion = "22"
def graalVmVendor = "GraalVM Community"
//def graalVmVendor = "Oracle Corporation"
if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
    graalVmVendor = "BellSoft"
}

graalvmNative {
    // This enables using separate GraalVM version thant the one used by gradle
    // However, this will only work properly if you only have only GraalVM JDKs installed on the machine.
    // Gradle will not be able to reliably detect GraalVM JDKs, nor detect GraalVM distributions from different vendors.
    toolchainDetection = true

    // Set to false to disable to run the tests in native mode.
    testSupport = true

    binaries {
        main {
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(graalVmVersion)
                vendor = JvmVendorSpec.matching(graalVmVendor)
            }

            imageName = 'libtika_native'

            // To build a shared lib, make sure to not set the mainClass
            sharedLibrary = true

            // The --no-fallback option to native-image causes the utility to fail if it can not create the image.
            fallback = false

            buildArgs.addAll(
                    "-H:+AddAllCharsets", // Very important to get UTF8 working
                    "-Ob",
                    "--parallelism=$numThreads",
                    "-march=compatibility" // This enable optimization on the current cpu architecture
            )
            jvmArgs.add('-Djava.awt.headless=true')
            requiredVersion = '21' // The minimal GraalVM version, can be `MAJOR`, `MAJOR.MINOR` or `MAJOR.MINOR.PATCH`
        }
    }
}